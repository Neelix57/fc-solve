#!/usr/bin/perl

use strict;
use warnings;

package main;

use Getopt::Long;
use Carp;
use IO::Socket::UNIX;
use Fcntl qw(:flock);
use File::Path qw(rmtree);
use Data::Dumper;

use Test::Trap qw( trap $trap :flow:stderr(systemsafe):stdout(systemsafe):warn );

sub _getlines
{
    my $filename = shift;

    open my $in, "<", $filename
        or die "Cannot open '$filename' for slurping - $!";

    my @l = <$in>;

    close($in);

    chomp(@l);

    return \@l;
}

sub _slurp
{
    my $filename = shift;

    open my $in, "<", $filename
        or die "Cannot open '$filename' for slurping - $!";

    local $/;
    my $contents = <$in>;

    close($in);

    return $contents;
}

if (!defined($ENV{'FCS_PATH'}))
{
    Carp::confess("FCS_PATH is not defined.");
}

my $FCS_PATH = $ENV{FCS_PATH};

my $RANGE_TO_VERIFY_SOCKET_PATH = sprintf("%s/range-queue.sock", $FCS_PATH);

my $GLOBAL_CONFIG_PATH = "$FCS_PATH/global-params.txt";
my $SUMMARY_LOCK_PATH = "$FCS_PATH/summary.lock";
my $SUMMARY_FILE_PATH = "$FCS_PATH/summary.txt";
my $FAULT_FILE_PATH = "$FCS_PATH/FAULT.txt";
my $SYNC_LOCK_PATH = "$FCS_PATH/sync.lock";
my $SYNC_FILE_PATH = "$FCS_PATH/sync.txt";
my $RESULTS_DIRS_BASE_PATH = "$FCS_PATH/results";

my $RANGE_SOLVER_EXE = "$FCS_PATH/freecell-solver-range-parallel-solve";

my $SOLVER_STEP = 1;

my $conf_lines = eval { _getlines($GLOBAL_CONFIG_PATH) };

if ($@)
{
    _mode_setup();
    exit(0);
}

my ($max_deal_idx, $preset_conf, $min_idx) = @$conf_lines;

my @preset = split(/\s+/, $preset_conf);

my @VERIFY_RANGE_COMMAND_START =
(
    $^X, "scripts/verify-range-in-dir-and-collect-stats.pl",
    '--summary-lock', $SUMMARY_LOCK_PATH,
    '--summary-file', $SUMMARY_FILE_PATH,
    @preset,
);

# The lock needs to be kept.
sub _update_sync_file
{
    my $callback = shift;

    my $lines = _getlines($SYNC_FILE_PATH);

    my %data;

    my @order = qw(NEXT_DEAL_IDX NEXT_ID CONTINUE);
    @data{@order} = @$lines;

    $callback->(\%data);

    open my $write_to_sync, '>', $SYNC_FILE_PATH
        or Carp::confess("Cannot open sync file - $!");

    print {$write_to_sync} map { $data{$_} , "\n" } @order;
     
    close ($write_to_sync);
}

my $mode;

my $num_solving_processes;

GetOptions(
    'mode=s' => \$mode,
    'num-solvers=i' => \$num_solving_processes,
) or die "Cannot set GetOptions";

if (!defined($num_solving_processes))
{
    die "Number of solving processes not specified.";
}

__PACKAGE__->can("_mode_$mode")->();

sub _write_file
{
    my $filename = shift;

    open my $out, '>', $filename
        or Carp::confess("Cannot open '$filename' for writing - $!");

    print {$out} @_;

    close($out);

    return;
}

sub _mode_setup
{
    foreach my $empty_fn ($SUMMARY_LOCK_PATH, $SYNC_LOCK_PATH, )
    {
        _write_file($empty_fn, '');
    }

    my $start_from = $ENV{FCS_MIN_DEAL_IDX};

    _write_file($GLOBAL_CONFIG_PATH, 
        "$ENV{FCS_MAX_DEAL_IDX}\n$ENV{FCS_PRESET_CONFIG}\n$start_from\n"
    );

    _write_file($SYNC_FILE_PATH, "$start_from\n1\n1\n");

    if (!mkdir($RESULTS_DIRS_BASE_PATH))
    {
        Carp::confess("Unable to mkdir '$RESULTS_DIRS_BASE_PATH' - $!");
    }

    return;
}

my $NUM_SOLVER_DEALS_PER_BATCH = 50;

sub _mode_solve
{
    my $continue = 1;

    MAIN_LOOP:
    while ($continue)
    {
        my $next_id;
        my $start_deal_idx;
        my $end_deal_idx;

        _lock_sync(
            sub {
                _update_sync_file(
                    sub {
                        my $data = shift;

                        if (! $data->{'CONTINUE'})
                        {
                            $continue = 0;
                            return;
                        }
                        elsif ($data->{NEXT_DEAL_IDX} > $max_deal_idx)
                        {
                            $continue = 0;
                            return;
                        }
                        else
                        {
                            $next_id = ($data->{NEXT_ID}++);
                            $start_deal_idx = $data->{NEXT_DEAL_IDX};
                            $end_deal_idx = $start_deal_idx + $NUM_SOLVER_DEALS_PER_BATCH;

                            if ($end_deal_idx > $max_deal_idx)
                            {
                                $end_deal_idx = $max_deal_idx;
                            }

                            $data->{NEXT_DEAL_IDX} = $end_deal_idx+1;
                            return;
                        }
                    },
                );
            }
        );

        if (! $continue)
        {
            last MAIN_LOOP;
        }

        my $dir_path = sprintf(
            "%s/results-%09d", $RESULTS_DIRS_BASE_PATH, $next_id
        );

        if (!mkdir($dir_path))
        {
            my $err = $!;

            # TODO : duplicate code from elsewhere.
            _lock_sync(
                sub {
                    open my $fault_fh, '>>', $FAULT_FILE_PATH
                        or Carp::confess("cannot open fault file - $!");

                    print {$fault_fh} "\n\n\nCannot mkdir '$dir_path' - '$err';\n";
                    close ($fault_fh);

                    _halt_process();
                }
            );

            $continue = 0;
            last MAIN_LOOP;
        }

        my $exit_code;

        trap
        {
            $exit_code = system(
                $RANGE_SOLVER_EXE, $start_deal_idx, $end_deal_idx, $SOLVER_STEP,
                '--solutions-directory', $dir_path,
                @preset,
                '-p', '-t', '-sam',
            );
        };

        my $stdout = $trap->stdout;
        my $stderr = $trap->stderr;

        if (!$exit_code)
        {
            # TODO : duplicate code from elsewhere.
            _lock_sync(
                sub {
                    open my $fault_fh, '>>', $FAULT_FILE_PATH
                        or Carp::confess("cannot open fault file - $!");

                    print {$fault_fh} "\n\n\n[Running the range parallel solver] stderr=\n<<<\n$stderr\n>>>\n\nstdout=\n<<<\n$stdout\n>>>\n";
                    close ($fault_fh);

                    _halt_process();
                }
            );

            $continue = 0;
            last MAIN_LOOP;
        }

        my $sock = IO::Socket::UNIX->new(
            Type => SOCK_STREAM(),
            Peer => $RANGE_TO_VERIFY_SOCKET_PATH,
        );

        if (! $sock )
        {
            print "Nothing to listen to.\n";
        }

        print {$sock} "ENQ\n$dir_path\n$start_deal_idx\n$end_deal_idx\n";
        $sock->flush;

        close($sock);
    }

    my $sock = IO::Socket::UNIX->new(
        Type => SOCK_STREAM(),
        Peer => $RANGE_TO_VERIFY_SOCKET_PATH,
    );

    if (! $sock )
    {
        print "Nothing to listen to.\n";
    }

    print {$sock} "FINISH\n";
    $sock->flush;

    close($sock);

    return;
}

sub _mode_queue_server
{
    my @queue;

    # To be set by the solving process that encounters the max_idx.
    my $num_finished = 0;

    my $server = IO::Socket::UNIX->new(
            Type => SOCK_STREAM(),
            Local => $RANGE_TO_VERIFY_SOCKET_PATH,
            Listen => 1,
        )
        or Carp::confess("Cannot listen to socket at '$RANGE_TO_VERIFY_SOCKET_PATH' - $!");

    my @order = qw(path min_idx max_idx);
    while (my $client = $server->accept)
    {
        my $read_line = sub {
            my $s = <$client>;
            chomp($s);

            return $s;
        };

        my $write = sub {
            print {$client} map { "$_\n" } @_;

            return;
        };

        my $cmd = $read_line->();

        if ($cmd eq "GET_NEXT")
        {
            if (@queue)
            {
                my $item = shift(@queue);
                $write->("DATA", @{$item}{@order});
            }
            elsif ($num_finished == $num_solving_processes)
            {
                $write->("FINISH");
            }
            else
            {
                $write->("WAIT");
            }
        }
        elsif ($cmd eq "ENQ")
        {
            my @data;

            foreach my $idx (0 .. $#order)
            {
                push @data, $read_line->();
            }

            push @queue, +{ map { $order[$_] => $data[$_] } (0 .. $#order) };
        }
        elsif ($cmd eq "FINISH")
        {
            $num_finished++;
        }

        close($client);
    }
    continue
    {
        print "QUEUE == " . Dumper([\@queue]) . "\n\n";
    }
}

sub _halt_process {
    _update_sync_file(
        sub {
            my $data = shift;

            $data->{CONTINUE} = 0;

            return;
        }
    );

    return;
}

sub _lock_sync {
    my $callback = shift;

    open my $lock_fh, ">", $SYNC_LOCK_PATH
        or Carp::confess ("Cannot lock summary-lock - $!");

    flock ($lock_fh, LOCK_EX) 
        or Carp::confess("Cannot lock summary lock - $!");

    $callback->();

    close ($lock_fh);
}

sub _mode_verify
{
    my $continue = 1;

    MAIN_LOOP:
    while ($continue)
    {
        my $sock = IO::Socket::UNIX->new(
            Type => SOCK_STREAM(),
            Peer => $RANGE_TO_VERIFY_SOCKET_PATH,
        );

        if (! $sock )
        {
            print "Nothing to listen to.\n";
            last MAIN_LOOP;
        }

        print {$sock} "GET_NEXT\n";
        $sock->flush;

        my $read_line = sub {
            my $s = <$sock>;
            chomp($s);

            return $s;
        };

        my $status = $read_line->();

        if ($status eq "WAIT")
        {
            close($sock);
            sleep(15);
        }
        elsif ($status eq "FINISH")
        {
            close($sock);
            $continue = 0;
        }
        elsif ($status eq "DATA")
        {
            my $path_to_dir = $read_line->();
            my $min_idx = $read_line->();
            my $max_idx = $read_line->();
            
            close($sock);

            my $exit_code;
            trap
            {
                $exit_code = system(
                    @VERIFY_RANGE_COMMAND_START,
                    '--min-idx', $min_idx, '--max-idx', $max_idx,
                    # Finally the path of the directory to process.
                    $path_to_dir,
                );
            };

            my $stderr = $trap->stderr;
            my $stdout = $trap->stdout;

            if ($exit_code != 0)
            {
                _lock_sync(
                    sub {
                        open my $fault_fh, '>>', $FAULT_FILE_PATH
                            or Carp::confess("cannot open fault file - $!");

                        print {$fault_fh} "\n\n\nstderr=\n<<<\n$stderr\n>>>\n\nstdout=\n<<<\n$stdout\n>>>\n";
                        close ($fault_fh);

                        _halt_process();
                    }
                );
            }
            else
            {
                rmtree( $path_to_dir, {});
            }
        }
    }

    return;
}

1;

